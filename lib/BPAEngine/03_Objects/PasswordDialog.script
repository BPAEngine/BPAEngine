
# -------------------------------------------------------------------------
# PasswordDialog simple/box
# -------------------------------------------------------------------------

PasswordDialog = fun (config)
  {
    new_object =  [] | global.PasswordDialog | global.Dialog(config);
    new_object.class = "PasswordDialog";

    new_object.subtext_config.z =  10; # DIALOG_Z + 10
    new_object.subtext_config.font_color = new_object.prompt_config.font_color;
    new_object.subtext_config.font_type  = "Sans";
    new_object.subtext_config.font_size  = new_object.prompt_config.font_size * 0.7;
    new_object.subtext = Text(new_object.subtext_config);

    if (new_object.mode == "window") {
      new_object.lock_config.image_path = config.lock_config.image_path  | "static/password_dialog/black_lock.png";
    } else {
      new_object.lock_config.image_path = config.lock_config.image_path  | "static/password_dialog/white_lock.png";
    }

    new_object.lock_config.z =  10; # DIALOG_Z + 10
    new_object.lock_icon = StaticImage(new_object.lock_config);

    return new_object;
  } | [];

PasswordDialog.Build = fun ()
  {
    Add(dimmer);
    Add(prompt);
    Add(input_box);
    Add(input_text);
    Add(subtext);
    if (mode == "window") {
      Add(window);
    }
  };

PasswordDialog.Prompt = fun (text, bullets_size)
  {
    AlignToObject(world, "cm", "cm");
    dimmer.AlignToObject(world, "lt", "lt");
    local.text_length = String.Size(text);
    if (text_length > 34)
    {
      local.new_text = String.Truncate(text, 37);
      local.subtext_lines = String.SplitByFixedSize(new_text.SubString(35, text_length), 40);

      subtext.SetText(String.Join(subtext_lines, "\n")).AlignToObject(this, "ct", "cb");

      prompt.SetText(new_text).AlignToObject(subtext, "ct", "cb").MoveY(10);
    }
    else {
      prompt.SetText(text).AlignToObject(this, "ct", "cb");
    }

    lock_icon.AlignToObject(prompt, "ct", "cb").MoveY(-10);

    if (this.mode == "window") {
      local.window_height = prompt.GetHeight() + lock_icon.GetHeight() + input_box.GetHeight() + 80;
      if (text_length > 34) {
        window_height += subtext.GetHeight() + 10;
      }
      window.SetHeight(window_height).SetWidth(prompt.GetWidth() + 80).AlignToObject(this, "cm", "cm");
      input_box.SetWidth(prompt.GetWidth());
    }

    input_box.AlignToObject(this, "cb", "ct").MoveY(30);

    input_text.SetText(String.Bullets(bullets_size), input_box.GetInnerWidth()).AlignToObject(input_box, "input", "lm");

    Show();
    if (text_length <= 34)  {
      subtext.Hide();
    }
  };
