
# -------------------------------------------------------------------------
# Static BPAE.Background
# -------------------------------------------------------------------------

BPAE.Background = fun (config)
  {
    new_object =  [] | global.BPAE.Background | global.BPAE.Object(config);
    new_object.class = "BPAE.Background";
    new_object.alignment = config.alignment | ["cm", "cm"];
    new_object.z = config.z | BPAE.BACKGROUND_Z;
    new_object.downscale = config.downscale | TRUE;
    new_object.upscale = config.upscale | FALSE;
    new_object.scale_factor = 1.0;
    new_object.image = Image(config.image_path | "static/backgrounds/background.png");
    new_object.image_ratio = new_object.image.GetHeight() / new_object.image.GetWidth();
    return new_object;
  } | [];

BPAE.Background.Build = fun (index)
  {
    ScaleFactor(world);

    local.scaled_image = image;
    if ((downscale && scale_factor < 1.0) || (upscale && scale_factor > 1.0)) {
      local.scaled_image = image.Scale(image.GetWidth()  * scale_factor, image.GetHeight() * scale_factor);
    }

    sprite.SetImage(scaled_image);
    AlignToObject(world, alignment[0], alignment[1]);
  };

BPAE.Background.ScaleFactor = fun (the_world)
  {
    if (the_world.screen_ratio > image_ratio) # Screen ratio is taller than image ratio, we will match the screen height
    {
       this.scale_factor =  the_world.GetHeight() / image.GetHeight();
    }
    else  # Screen ratio is wider than image ratio, we will match the screen width
    {
       this.scale_factor =  the_world.GetWidth() / image.GetWidth();
    }
    return this.scale_factor;
  };
