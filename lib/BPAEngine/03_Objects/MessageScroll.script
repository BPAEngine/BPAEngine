
# -------------------------------------------------------------------------
# Message scroll
# -------------------------------------------------------------------------

MessageScroll = fun (config)
  {
    new_object =  [] | global.MessageScroll | global.Object(config);
    new_object.class = "MessageScroll";
    new_object.z = config.z | 1000;

    new_object.font_color = config.font_color | [0, 0, 0];
    new_object.font_type  = config.font_type | "Fixed";
    new_object.font_size  = config.font_size | 10;

    new_object.max_lines = config.max_lines | 10;
    new_object.max_width = config.max_width | 80;
    new_object.faded_lines = config.faded_lines | new_object.max_lines * 0.5;
    new_object.spacing = config.spacing | 1;

    for (local.i = 0; local.i < new_object.max_lines; local.i++) {
      new_object.lines[local.i] = ""; # Initialize the lines
      new_object.message_sprite[local.i] = Sprite();
      new_object.message_sprite[local.i].SetPosition(new_object.x, new_object.y + (local.i * new_object.font_size * (new_object.spacing + 1.0)), new_object.z);
    }
    return new_object;
  } | [];

MessageScroll.Add = fun (message)
  {
    local.message_lines = String.SplitByFixedSize(message, max_width);
    for (local.i = 0; local.i < Array.Size(local.message_lines); local.i++) {
      # Shift lines
      for (local.j = 0; local.j < max_lines - 1; local.j++) {
        lines[local.j] = lines[local.j+1];
      }

      lines[max_lines - 1] = Image.Text(local.message_lines[local.i], font_color[0], font_color[1], font_color[2], 1, font_type + " " + font_size);
    }

    # Re-assing the text images
    for (local.i = 0; local.i < max_lines; local.i++) {
      message_sprite[local.i].SetImage(lines[local.i]);
    }
  };

MessageScroll.Update = fun (delta)
  {
    # Avoid updating coordinates, animations and so on
  };

MessageScroll.SetSpritesOpacity = fun (new_opacity)
  {
    for (local.i = 0; local.i < max_lines; local.i++)
    {
      message_sprite[local.i].SetOpacity(new_opacity);
    }
  };

MessageScroll.Show = fun ()
  {
    local.opacity = 1;
    for (local.i = 0; local.i < max_lines; local.i++) {
      if (local.i < faded_lines)
        local.opacity = 1.2 - (faded_lines - local.i) / faded_lines;
      message_sprite[local.i].SetOpacity(local.opacity);
    }
    return this;
  };

MessageScroll.Build = fun ()
  {
    Show();
  };

MessageScroll.SetX = fun (x)
  {
    this.x = x;
    for (local.i = 0; local.i < max_lines; local.i++) {
      message_sprite[local.i].SetX(x);
    }
    return this;
  };

MessageScroll.SetY = fun (y)
  {
    this.y = y;
    for (local.i = 0; local.i < max_lines; local.i++) {
      message_sprite[local.i].SetY(y + (local.i * font_size * (spacing + 1.0)));
    }
    return this;
  };

MessageScroll.SetZ = fun (z)
  {
    this.z = z;
    for (local.i = 0; local.i < max_lines; local.i++) {
      message_sprite[local.i].SetZ(z);
    }
    return this;
  };

MessageScroll.GetWidth = fun ()
  {
    return max_width * font_size;
  };

MessageScroll.GetHeight = fun ()
  {
    return max_lines * font_size * (spacing + 1.0);
  };
