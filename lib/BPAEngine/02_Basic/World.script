
# -------------------------------------------------------------------------
# World definition
# -------------------------------------------------------------------------

World = fun (config)
  {
    new_world = [] | global.World;
    new_world.background_colors = config.background_colors | [[0.8, 0.8, 0.8], [0.5, 0.5 ,0.5]];

    new_world.tick = 0;
    new_world.time = 0.0;
    new_world.delta = 0.0;
    new_world.speed = 1.0;

    new_world.objects = [];
    return new_world;
  } | [];

World.SetBackgroundColors = fun ()
  {
    Window.SetBackgroundTopColor(background_colors[0][0], background_colors[0][1], background_colors[0][1]);
    Window.SetBackgroundBottomColor(background_colors[1][0], background_colors[1][1], background_colors[1][1]);
  };

World.Build = fun ()
  {
    SetBackgroundColors();
    for (local.i = 0; local.i < Array.Size(objects); local.i++)
    {
      objects[local.i].Build();
    }
    Setup();
  };

World.Tick = fun ()
  {
    tick++;
  };

World.Update = fun (new_time, progress)
  {
    delta = (new_time - time) * speed;
    time = new_time;
    for (local.i = 0; local.i < Array.Size(objects); local.i++)
    {
      objects[local.i].Update(delta);
    }
  };

World.Add = fun (object)
  {
    if (object.class == "MessageScroll") this.message_scroll = object;
    Array.Add(objects, object);
  };

World.Setup = fun ()
  {
    # This should be overwritten in the theme code
  };

World.ProcessMessage = fun (message)
  {
    if (message_scroll) message_scroll.Add(message);
    Debug("ProcessMessage: " + message, 3);
  };

World.UpdateStatus = fun (status)
  {
    if (message_scroll) message_scroll.Add(status);
    Debug("UpdateStatus: " + status, 4);
  };

World.DisplayQuestion = fun (prompt, entry)
  {
    Debug("DisplayQuestion: " + prompt + ": " + entry, 5);
  };

World.AskPassword = fun (prompt, bullets_size)
  {
    Debug("AskPassword: " + prompt + ": " + String.Bullets(bullets_size), 5);
  };
