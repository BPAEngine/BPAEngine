
# -------------------------------------------------------------------------
# Sprite Animations
# -------------------------------------------------------------------------

BPAE.SpriteAnimation = fun (config)
  {
    new_animation = [] | global.BPAE.SpriteAnimation;
    new_animation.filename = config.filename | "animation-";
    new_animation.digits = config.digits | FALSE;
    new_animation.start_frame = config.start_frame | 0;
    new_animation.end_frame = config.end_frame | 0;
    new_animation.length = config.length | 1.0;
    new_animation.loop = config.loop | -1;
    new_animation.speed = config.speed | 1.0;

    new_animation.frames_number = new_animation.end_frame - new_animation.start_frame + 1;
    new_animation.frame_duration = new_animation.length / new_animation.frames_number;
    new_animation.running = 0;
    new_animation.actual_time = 0.0;
    new_animation.actual_frame = 0;
    new_animation.frames = [];

    return new_animation;
  } | [];

BPAE.SpriteAnimation.LoadImages = fun ()
  {
    local.base_path = "sprites/" + filename;
    for (local.i = start_frame; local.i <= end_frame; local.i++)
    {
      local.string_index = "" + i;
      if (digits) {
        local.zeros_needed = digits - String.Size(string_index);
        for (local.j = 0; local.j < zeros_needed; local.j++) {
          string_index = "0" + string_index;
        }
      }
      frames[i-start_frame] = Image(base_path + string_index + ".png");
    }
  };

BPAE.SpriteAnimation.Update = fun (delta, theta)
  {
    if (running)
    {
      actual_time += delta * speed;
      if (actual_time >= length)
      {
        if (loop != 0)
        {
          if (loop > 0)
            loop--;
          Play(0.0);
        }
        else
        {
          Stop();
          return;
        }
      }
      actual_frame = Math.Clamp(Math.Int(actual_time / frame_duration), 0, frames_number - 1);
      if (sprite)
        sprite.SetImage(frames[actual_frame].Rotate(theta));
    }
  };

BPAE.SpriteAnimation.Play = fun (start)
  {
    actual_time = start | actual_time;
    running = 1;
  };

BPAE.SpriteAnimation.Pause = fun ()
  {
    running = 0;
  };

BPAE.SpriteAnimation.Stop = fun ()
  {
    actual_time = 0.0;
    running = 0;
  };
