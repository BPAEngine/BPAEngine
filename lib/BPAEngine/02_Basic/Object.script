
# -------------------------------------------------------------------------
# Object definition
# -------------------------------------------------------------------------

Object = fun (config, id)
  {
    new_object = [] | global.Object;
    new_object.id = id | "id_" + Math.Random() * 10000;
    new_object.class = "Object";

    new_object.x = config.x | 0;
    new_object.y = config.y | 0;
    new_object.z = config.z | 1;

    new_object.theta = config.theta | 0.0; # Starting angle for image rotation in degrees
    new_object.spin = config.spin | 0.0; # For image rotation

    new_object.orientation = config.orientation | 0.0; # In degrees
    new_object.speed = config.speed | 0.0;

    new_object.speed_variator = config.speed_variator;
    new_object.orientation_variator = config.orientation_variator;

    new_object.animations = [];
    new_object.sprite = Sprite();
    new_object.sprite.SetPosition(new_object.x, new_object.y, new_object.z);
    return new_object;
  } | [];

Object.To_String = fun ()
  {
    return class + "{id:"+id+", x:"+x+", y:"+y+", z:"+z+", speed:"+speed+", orientation:"+orientation+"}";
  };

Object.Update = fun (delta)
  {
    theta += spin;
    for (local.i = 0; local.i < Array.Size(animations); local.i++)
    {
      animations[local.i].Update(delta, theta);
    }

    if (orientation_variator)
    {
      orientation = orientation_variator.Next(orientation, delta);
    }
    if (speed_variator)
    {
      speed = speed_variator.Next(speed, delta);
    }

    local.angle = ((orientation + 90.0) * Math.Pi) / 180.0;
    x += speed * Math.Sin(local.angle);
    y += speed * Math.Cos(local.angle);
    sprite.SetPosition(x, y, z);
  };

Object.GetX = fun ()
  {
    return x;
  };

Object.GetY = fun ()
  {
    return y;
  };

Object.SetX = fun (x)
  {
    this.x = x;
    sprite.SetX(x);
  };

Object.SetY = fun (y)
  {
    this.y = y;
    sprite.SetY(y);
  };

Object.SetZ = fun (z)
  {
    this.z = z;
    sprite.SetZ(z);
  };

Object.Set2DPosition = fun (x, y)
  {
    SetX(x);
    SetY(y);
  };

Object.Set3DPosition = fun (x, y, z)
  {
    SetX(x);
    SetY(y);
    SetZ(z);
  };

Object.MoveX = fun (dx)
  {
    SetX(x + dx);
  };

Object.MoveY = fun (dy)
  {
    SetY(y + dy);
  };

Object.Move2DPosition = fun (dx, dy)
  {
    MoveX(dx);
    MoveY(dy);
  };

Object.GetWidth = fun ()
  {
    sprite.GetImage().GetWidth();
  };

Object.GetHeight = fun ()
  {
    sprite.GetImage().GetHeight();
  };

Object.AlignToObject = fun (object, object_anchor, my_anchor)
  {
    local.object_anchor_x = object_anchor.CharAt(0); # Can be "l", "c", "r"
    local.object_anchor_y = object_anchor.CharAt(1); # Can be "t", "m", "b"
    local.my_anchor_x = my_anchor.CharAt(0);
    local.my_anchor_y = my_anchor.CharAt(1);
    local.dx = 0.0;
    local.dy = 0.0;
    if (local.my_anchor_x == "c") local.dx -= GetWidth() / 2.0;
    if (local.my_anchor_x == "r") local.dx -= GetWidth();
    if (local.object_anchor_x == "c") local.dx += object.GetWidth() / 2.0;
    if (local.object_anchor_x == "r") local.dx += object.GetWidth();
    if (local.my_anchor_y == "m") local.dy -= GetHeight() / 2.0;
    if (local.my_anchor_y == "b") local.dy -= GetHeight();
    if (local.object_anchor_y == "m") local.dy += object.GetHeight() / 2.0;
    if (local.object_anchor_y == "b") local.dy += object.GetHeight();
    Set2DPosition(object.GetX() + local.dx, object.GetY()  + local.dy);
  };

Object.PlayAnimation = fun (index)
  {
    animations[index].sprite = sprite;
    animations[index].Play();
  };

Object.Build = fun (index)
  {
  };

Object.Hide = fun ()
  {
    sprite.SetOpacity(0);
  };

Object.Show = fun ()
  {
    sprite.SetOpacity(1);
  };
